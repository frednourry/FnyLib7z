apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}
artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

task currentDir {
    println 'Current path: ' + file('.')
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll setup later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = "Android library based on 7-zip sources that allows you to open and compress files using paths or URIs"
                    url = 'https://github.com/frednourry/FnyLib7z'
                    licenses {
                        license {
                            name = 'GNU Lesser General Public License'
                            url = 'https://www.gnu.org/licenses/lgpl-3.0.html'
                        }
                    }
                    developers {
                        developer {
                            id = 'frednourry'
                            name = 'Frederic Nourry'
                            email = 'frederic.nourry.dev@gmail.com'
                        }
                    }

                    // Version control info
                    scm {
                        connection = 'scm:https://github.com/frednourry/FnyLib7z.git'
                        developerConnection = 'scm:git@github.com:frednourry/FnyLib7z.git'
                        url = 'https://github.com/frednourry/FnyLib7z'
                    }
                }
            }
        }
    }
}

signing {
/*  // Useless according to https://stackoverflow.com/questions/68447837/gradle-signing-android-library-publications-cannot-perform-signing-task-because
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
//            rootProject.ext["signing.key"],
            rootProject.ext["signing.secretKeyRingFile"],
            rootProject.ext["signing.password"]
    )*/
    sign publishing.publications
}
